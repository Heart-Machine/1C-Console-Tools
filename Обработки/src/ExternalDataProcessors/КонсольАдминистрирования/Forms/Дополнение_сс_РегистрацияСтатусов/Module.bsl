
&НаКлиенте
Процедура УказатьПериод(Команда)

	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Новый СтандартныйПериод(ДатаНачала, ДатаОкончания);
	Если Диалог.Редактировать() Тогда
	    ДатаНачала = НачалоДня(Диалог.Период.ДатаНачала);
	    ДатаОкончания = КонецДня(Диалог.Период.ДатаОкончания);
		ПолучитьКоличествоЗаписей();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	ПолучитьКоличествоЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	ПолучитьКоличествоЗаписей();
КонецПроцедуры

&НаСервере
Процедура ПолучитьКоличествоЗаписей()
	Выборка = СформироватьЗапрос(Перечисления.сс_Статус.Создан);
	КоличествоЗаписей = Выборка.Количество();	
	//Выборка = СформироватьЗапрос(Перечисления.сс_Статус.ВернулиОК);
	//КоличествоЗаписей = КоличествоЗаписей + Выборка.Количество();
КонецПроцедуры

&НаСервере
Функция СформироватьЗапрос(Статус)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Документ,
		|	ЕСТЬNULL(сс_РегистрацияСтатусовСрезПоследних.Статус, НЕОПРЕДЕЛЕНО) КАК Статус,
		|	РеализацияТоваровУслуг.Ответственный КАК Ответственный
		|ПОМЕСТИТЬ ВТ_Реализации
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сс_РегистрацияСтатусов.СрезПоследних(, Статус = &Статус) КАК сс_РегистрацияСтатусовСрезПоследних
		|		ПО РеализацияТоваровУслуг.Ссылка = сс_РегистрацияСтатусовСрезПоследних.Документ
		|			И (сс_РегистрацияСтатусовСрезПоследних.Вид = ЗНАЧЕНИЕ(перечисление.сс_ВидыРегистрацииСтатусов.УПД))
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата >= &ДатаНачала
		|	И РеализацияТоваровУслуг.Дата <= &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Реализации.Документ КАК Документ,
		|	ВТ_Реализации.Ответственный КАК Ответственный,
		|	ВТ_Реализации.Статус КАК Статус
		|ИЗ
		|	ВТ_Реализации КАК ВТ_Реализации
		|ГДЕ
		|	ВТ_Реализации.Статус = НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Статус", Статус);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выгрузить();	
	Возврат Выборка;

КонецФункции // СформироватьЗапрос()

&НаСервере
Процедура ВосстановитьСтатусыСозданияНаСервере()
	ВремяНачала = ТекущаяДата();
	Сообщить("Начало выполнения: " + Формат(ВремяНачала,"ДЛФ=T"));
	Статус = Перечисления.сс_Статус.Создан;
	РегДоп = РегистрыСведений.сс_РегистрацияСтатусов.СоздатьНаборЗаписей();
	Для каждого Строка Из ТаблЖурналРегистрации Цикл
		Запись = РегДоп.Добавить();
		Запись.Период = Строка.Дата;
	    Запись.Документ = Строка.Данные;
		Запись.Вид = Перечисления.сс_ВидыРегистрацииСтатусов.УПД;
		Запись.Статус = Статус;
		Запись.Пользователь = ?(ЗначениеЗаполнено(Строка.Пользователь),
								Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", Строка.Пользователь),
								Неопределено);
	КонецЦикла;
	РегДоп.Записать(Ложь);
	Сообщить("Конец выполнения: " + Формат(ТекущаяДата(),"ДЛФ=T")); 
	Сообщить("Длительность выполнения: " + (ТекущаяДата() - ВремяНачала) + " сек."); 
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСтатусыСоздания(Команда)
	ВосстановитьСтатусыСозданияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолучитьКоличествоЗаписей();
КонецПроцедуры

&НаСервере
Процедура ОтборЖурналаРегистрацийСозданияНаСервере()
	ВремяНачала = ТекущаяДата();
	Сообщить("Начало выполнения: " + Формат(ВремяНачала,"ДЛФ=T"));
	ТаблЖурналРегистрации.Очистить();
	Статус = Перечисления.сс_Статус.Создан;
	Выборка = СформироватьЗапрос(Статус);
	НоваяТаблица = Новый ТаблицаЗначений;
	Для каждого Строка Из Выборка Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("Метаданные", Метаданные.Документы.РеализацияТоваровУслуг);
		Отбор.Вставить("Событие", "_$Data$_.New");
		Отбор.Вставить("Данные", Строка.Документ);
		ВыгрузитьЖурналРегистрации(НоваяТаблица, Отбор);
		Если НоваяТаблица.Количество() > 0 Тогда
		    НоваяСтрока = ТаблЖурналРегистрации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,НоваяТаблица[0]);
		Иначе
		    НоваяСтрока = ТаблЖурналРегистрации.Добавить();
			НоваяСтрока.Данные = Строка.Документ;
			НоваяСтрока.Дата = Строка.Документ.Дата;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоСозданныхДокументов = ТаблЖурналРегистрации.Количество();
	Сообщить("Конец выполнения: " + Формат(ТекущаяДата(),"ДЛФ=T")); 
	Сообщить("Длительность выполнения: " + (ТекущаяДата() - ВремяНачала) + " сек."); 
КонецПроцедуры

&НаКлиенте
Процедура ОтборЖурналаРегистрацийСоздания(Команда)
	ОтборЖурналаРегистрацийСозданияНаСервере();
КонецПроцедуры
