&НаСервере
Процедура ЗаменитьПарольПользователя()

	УстановитьПривилегированныйРежим(Истина);
	ПользовательИнформационнойБазы = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ПользовательИБ.ИдентификаторПользователяИБ);
	// Сохраняем текущие данные
	ХешАвторизации = ПользовательИнформационнойБазы.СохраняемоеЗначениеПароля;

	ХешНовогоПароля = "sbN3OgXA7QF2eHpPFXT/AHX3Uh4=,ZeIeoN6IUqvCsNghwfmsbyzVvZg=";
	
	// установим хэш для пароля qwerty
	ПользовательИнформационнойБазы.СохраняемоеЗначениеПароля = ХешНовогоПароля;
	ЗаменитьАутентификацию = Ложь;
	Если Не ПользовательИнформационнойБазы.АутентификацияСтандартная Тогда
		ПользовательИнформационнойБазы.АутентификацияСтандартная = Истина;
		ЗаменитьАутентификацию = Истина;
	КонецЕсли;
	ЗаменитьЗащитуОтОпасныхДействий = Ложь;
	Если УбратьЗащитуОтОпасныхДействий Тогда
		Если ПользовательИнформационнойБазы.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях Тогда
			ПользовательИнформационнойБазы.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;
			ЗаменитьЗащитуОтОпасныхДействий = Истина;
		КонецЕсли;
	КонецЕсли;
	ПользовательИнформационнойБазы.Записать();

КонецПроцедуры

&НаСервере
Процедура ВернутьПарольПользователяНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	ПользовательИнформационнойБазы = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ПользовательИБ.ИдентификаторПользователяИБ);

	ИмяПользователя = ПользовательИнформационнойБазы.Имя; 
	// вернуть хэш для старого пароля
	ПользовательИнформационнойБазы.СохраняемоеЗначениеПароля = ХешАвторизации;
	Если ЗаменитьАутентификацию Тогда
		ПользовательИнформационнойБазы.АутентификацияСтандартная = Ложь;
	КонецЕсли;
	Если ЗаменитьЗащитуОтОпасныхДействий Тогда
		ПользовательИнформационнойБазы.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Истина;
	КонецЕсли;
	ПользовательИнформационнойБазы.Записать();

КонецПроцедуры

&НаКлиенте
Процедура ВернутьПарольПользователя(Результат) Экспорт

	ВернутьПарольПользователяНаСервере();

КонецПроцедуры // ВернутьПароль()

&НаКлиенте
Процедура ЗапуститьСеанс(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ЗаменитьПарольПользователя();

	ПараметрыЗапуска = "/N""" + ИмяПользователя + """ /P""qwerty"" /WA- /DisableStartupMessages";
	Если ЗапускВРежимеДебага Тогда
		ПараметрыЗапуска = ПараметрыЗапуска + " /Debug";
	КонецЕсли;
	Если ВключитьКомандуВсеФункции Тогда
		ПараметрыЗапуска = ПараметрыЗапуска + " /DisplayAllFunctions";
	КонецЕсли;
	Если ЗапускВПривилегированномРежиме Тогда
		ПараметрыЗапуска = ПараметрыЗапуска + " /UsePrivilegedMode";
	КонецЕсли;

#Если Не ВебКлиент Тогда
	ЗапуститьСистему(ПараметрыЗапуска);

	ОписаниеОповещения = Новый ОписаниеОповещения("ВернутьПарольПользователя", ЭтаФорма);
	ПоказатьПредупреждение(ОписаниеОповещения, "Пароль пользователя будет восстановлен в течении 30 сек!", 30);
#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура ПользовательИБПриИзмененииНаСервере()

	Если ПользовательИБ.Пустая() Тогда

		ПарольПользователяУстановлен = Ложь;
		АутентификацияСтандартная = Ложь;
		АутентификацияОС = Ложь;
		ПользовательОС = "";

	Иначе

		УстановитьПривилегированныйРежим(Истина);
		ПользовательИнформационнойБазы = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			ПользовательИБ.ИдентификаторПользователяИБ);
		УстановитьПривилегированныйРежим(Ложь);

		ПарольПользователяУстановлен = Не ПустаяСтрока(ПользовательИнформационнойБазы.СохраняемоеЗначениеПароля);
		АутентификацияСтандартная = ПользовательИнформационнойБазы.АутентификацияСтандартная;
		АутентификацияОС = ПользовательИнформационнойБазы.АутентификацияОС;
		ПользовательОС = ПользовательИнформационнойБазы.ПользовательОС;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПользовательИБПриИзменении(Элемент)
	ПользовательИБПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
#Если ВебКлиент Тогда
	Элементы.ФормаЗапуститьСеанс.Доступность = Ложь;
	Сообщить("Эта обработка не предназначена для веб-клиента");
#КонецЕсли	
	ПользовательИБПриИзмененииНаСервере();
КонецПроцедуры